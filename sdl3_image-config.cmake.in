# sdl3_image cmake project-config input for ./configure scripts

include(FeatureSummary)
set_package_properties(SDL3_image PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_image/"
    DESCRIPTION "SDL_image is an image file loading library"
)

set(SDL3_image_FOUND TRUE)

set(SDL3IMAGE_AVIF  @LOAD_AVIF@)
set(SDL3IMAGE_BMP   @LOAD_BMP@)
set(SDL3IMAGE_GIF   @LOAD_GIF@)
set(SDL3IMAGE_JPG   @LOAD_JPG@)
set(SDL3IMAGE_JXL   @LOAD_JXL@)
set(SDL3IMAGE_LBM   @LOAD_LBM@)
set(SDL3IMAGE_PCX   @LOAD_PCX@)
set(SDL3IMAGE_PNG   @LOAD_PNG@)
set(SDL3IMAGE_PNM   @LOAD_PNM@)
set(SDL3IMAGE_QOI   @LOAD_QOI@)
set(SDL3IMAGE_SVG   @LOAD_SVG@)
set(SDL3IMAGE_TGA   @LOAD_TGA@)
set(SDL3IMAGE_TIF   @LOAD_TIF@)
set(SDL3IMAGE_XCF   @LOAD_XCF@)
set(SDL3IMAGE_XPM   @LOAD_XPM@)
set(SDL3IMAGE_XV    @LOAD_XV@)
set(SDL3IMAGE_WEBP  @LOAD_WEBP@)

set(SDL3IMAGE_JPG_SAVE @SDL3IMAGE_JPG_SAVE@)
set(SDL3IMAGE_PNG_SAVE @SDL3IMAGE_PNG_SAVE@)

set(SDL3IMAGE_VENDORED  FALSE)

set(SDL3IMAGE_BACKEND_IMAGEIO   @USE_IMAGEIO@)
set(SDL3IMAGE_BACKEND_STB       @USE_STBIMAGE@)
set(SDL3IMAGE_BACKEND_WIC       @USE_WIC@)

get_filename_component(prefix "${CMAKE_CURRENT_LIST_DIR}/@cmake_prefix_relpath@" ABSOLUTE)
set(exec_prefix "@exec_prefix@")
set(bindir "@bindir@")
set(includedir "@includedir@")
set(libdir "@libdir@")
set(_sdl3image_extra_static_libraries "@IMG_LIBS@ @PC_LIBS@")
string(STRIP "${_sdl3image_extra_static_libraries}" _sdl3image_extra_static_libraries)

set(_sdl3image_bindir   "${bindir}")
set(_sdl3image_libdir   "${libdir}")
set(_sdl3image_incdir   "${includedir}/SDL3")

# Convert _sdl3image_extra_static_libraries to list and keep only libraries
string(REGEX MATCHALL "(-[lm]([-a-zA-Z0-9._]+))|(-Wl,[^ ]*framework[^ ]*)" _sdl3image_extra_static_libraries "${_sdl3image_extra_static_libraries}")
string(REGEX REPLACE "^-l" "" _sdl3image_extra_static_libraries "${_sdl3image_extra_static_libraries}")
string(REGEX REPLACE ";-l" ";" _sdl3image_extra_static_libraries "${_sdl3image_extra_static_libraries}")

unset(prefix)
unset(exec_prefix)
unset(bindir)
unset(includedir)
unset(libdir)

include(CMakeFindDependencyMacro)

if(NOT TARGET SDL3_image::SDL3_image)
    add_library(SDL3_image::SDL3_image SHARED IMPORTED)
    set_target_properties(SDL3_image::SDL3_image
        PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${_sdl3image_incdir}"
            COMPATIBLE_INTERFACE_BOOL "SDL3_SHARED"
            INTERFACE_SDL3_SHARED "ON"
    )
    if(WIN32)
        set_target_properties(SDL3_image::SDL3_image
            PROPERTIES
                IMPORTED_LOCATION "${_sdl3image_bindir}/SDL3_image.dll"
                IMPORTED_IMPLIB "${_sdl3image_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL3_image.dll${CMAKE_STATIC_LIBRARY_SUFFIX}"
        )
    else()
        set_target_properties(SDL3_image::SDL3_image
            PROPERTIES
                IMPORTED_LOCATION "${_sdl3image_libdir}/${CMAKE_SHARED_LIBRARY_PREFIX}SDL3_image${CMAKE_SHARED_LIBRARY_SUFFIX}"
        )
    endif()
endif()

if(NOT TARGET SDL3_image::SDL3_image-static)
    add_library(SDL3_image::SDL3_image-static STATIC IMPORTED)

    set_target_properties(SDL3_image::SDL3_image-static
        PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${_sdl3image_incdir}"
            IMPORTED_LOCATION "${_sdl3image_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL3_image${CMAKE_STATIC_LIBRARY_SUFFIX}"
            INTERFACE_LINK_LIBRARIES "${_sdl3image_extra_static_libraries}"
    )
endif()

unset(_sdl3image_extra_static_libraries)
unset(_sdl3image_bindir)
unset(_sdl3image_libdir)
unset(_sdl3image_incdir)
